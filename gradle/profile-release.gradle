import java.text.SimpleDateFormat

apply plugin: 'net.researchgate.release'

ext {
   snippetsDir = file("build/generated-snippets")
}

test { 
   outputs.dir snippetsDir
}
  
asciidoctor {
   attributes "snippets": snippetsDir,
            "version": jar.version,
            "date": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
   inputs.dir snippetsDir
   dependsOn test  
   sourceDir "docs/"
   outputDir "build/docs"
}

test {
   environment "spring.config.location", "classpath:/config/"
   include '**/*DataResourceControllerDocumentationTest*'
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

bootJar {
   manifest {
      attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
   }
   dependsOn asciidoctor
   from ("${asciidoctor.outputDir}/html5") {
      into 'static/docs'
   }
   launchScript()
}

/**The default task for compiling the project has to be overwritten in order to build first 
 * from a clean release
 */
compileJava {
    if (project.hasProperty('clean-release')) {
        apply from: 'gradle/git_checkout.gradle'
        dependsOn checkoutProject
    }
}

release {
    tagTemplate = 'v${version}'
    versionPropertyFile = 'gradle.properties'
    versionProperties = ['version', 'mainversion']
}